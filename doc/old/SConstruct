# Multiplatform
# Quake3e SCons Compiling setup
# :::::::::::::::::::::::::::::
# Based on: Quake3 Unix Makefile 
#         | Nov '98 | Zoid <zoid@idsoftware.com>
#         | Loki Hacking by Bernd Kreimeier
#                           Ryan C. Gordon
#                           Rafael Barrero
#                           ioq3 cr3w
# Ported to SCons by:
#   2022 | sOkam! for Opensource Defrag
#
#::::::::::::::::::::::::::::::::::::::
# This file intends to be a multiplatform buildsystem.
# Keep that in mind if you modify it.
# SCons takes some learning, but its way more powerful than Makefiles
#   TODO: Native support for win, macos
#::::::::::::::::::::::::::::::::::::::
#
# Naming Convention:
# ::::::::::::::::::
# cur___  : Current host
# trg___  : Build target
# rls___  : Release
# dbg___  : Debug
# win___  : Windows version
# lnx___  : Linux version
# env___  : Build environment
# src___  : Source code
# obj___  : Object built from source code
# __Name  : Name of X (string)
# __Dir   : Folder containing X
# sc___   : Scons related variable
# *tru*   : Related to Aliases for SCons expected values. i.e: truArch='w', scArch='win32'
# *valid* : Supported by this build system. Not valid = support is not implemented
#::::::::::::::::::::::::::::::::::::::
# Word Order:
#   Organization goes FIRST. Program organization, not idiomatic english.
#     Category -> Thing -> Differentiating Exception 
#     : catThing_excpt
#     Example:
#       English (wrong):     initialize_build_target
#       Organization first:  trgBuild_init
#::::::::::::::::::::::::::::::::::::::
# Case styling:
#   camelCase preferred 
#   PascalCase for classes/constants only, and for SCons builtin methods
#   snake_case discouraged, only for exception management
#   All-caps for SCons environment variables -only- (CCFLAGS, etc)
#     SCons variable -inputs- (defined here, passed to scons) SHOULD be named with snake case exception styling
#::::::::::::::::::::::::::::::::::::::

# Usage and Help
# :::::::::::::::
Help("""
: Action:     command              Description
::::::::::    ::::::::             ::::::::::::
                !cmd : not implemented
: Help:       scons -h             Shows this text.
: Verbose:    !scons -V             Make the output of scons fully verbose: Formatting removed and nothing filtered.
              TEMP: Use V=1        until bugfix. There is a reported issue in SCons. 
: Cores:      scons -j NUM         Use NUM of cores for building. Overwrites the value set in the scripts
: Clean:      scons -c             Cleans default build targets. Doesn't remove folders.
              scons -c NAME        Cleans selected build targets. Doesn't remove folders.
: Build:        
    Default   scons                Builds the targets set in the `build_default` list
    Game      scons game           Builds only the game code (dynamic library), without the engine   (sgame, cgame, ui)
    Debug     scons debug          Builds debug version only     (client+server+game).  Default when `build_default` is empty
    Engine    scons engine         Builds only the engine code, without any game code
    Server    !scons server         Builds only the dedicated server code (ded), without any client code
    Release   scons release        Builds release version only   (client+server+game)
    Distrib   scons distribute     Cross-compiles and builds release versions for all supported platforms.
    All       scons all            Builds all targets (including debug and release versions of both)
: Options:
    Platform  scons p=NAME         Cross-compilation for a platform different than the current host platform
                    platform=NAME   Valid names:                     # todo: darwin
                                    Win64: 'w', 'win'                : SCons 'win32':'64bit'
                                    Win32: 'w32', 'win32'            : SCons 'win32':'32bit'
                                    Linux: 'l', 'lnx', 'linux'       : SCons 'posix':'64bit'
                                    Linux: 'l32', 'lnx32', 'linux32' : SCons 'posix':'32bit'
    Arch      scons arch=NAME      Cross-compilation for an architecture other than the current host arch
                                    Don't use, unless you know what you are doing.     #TODO: x86, arm 
                                    Defaults to x86 or x86_64, if the host platform is of a different bitsize than the one provided. Else current host arch
                                    Uses SCons default names. Valid names: (64bit) 'amd64','x86_64'  (32bit) 'x86','arm'
: Configuration:
    ./scons_local.py              Allows the user to overwrite default configuration variables set it ./lib/scons_config.py
                                   Useful to make permanent changes locally, without losing progress during updates
                                   Copy the template file `./lib/skel/scons_local.py` as `./scons_local.py`, or create a new empty file
""")
#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


###############
# Helpers & imports
# ::::::::::::::::::
import lib.helper.scons as h
from lib.helper.scons import getLocal, getCur, getCli, getGlob, getBits, isVerbose, BuildObject, BuildInfo
from lib.helper.scons import ARCH, PLAT, BITS, TRG, scPlatforms, validPlatforms, validArchs
import sys; sys.dont_write_bytecode=True  # Do not create bytecode __pycache__ folder
from os.path import sep
#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


###############
# Configuration
# ::::::::::::::::
from lib.helper.config_scons import *

# For configuration different than the defaults, without losing changes during updates:
#   Create a new file named "scons_local.py" in the same directory as this one
#   Assign your configuration there
#   The values active in the file will overwrite any values set in lib.scons_config      
if h.exists('scons_local.py'): from scons_local import *

#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


###############
# Initialization
# ::::::::::::::::

# Define system
# ::::::::::::::
# Current system
curPlatform    = getCur(PLAT)
curArch        = getCur(ARCH) 
curBits        = getCur(BITS)
if not curPlatform or not curArch:     sys.exit(f'::ERR SCons failed to detect the host system: HOST_OS={curPlatform}, HOST_ARCH={curArch}') 
if curPlatform not in validPlatforms:  sys.exit(f'::ERR Compilation from host platform: {curPlatform}  is not currently supported') 
if curArch     not in validArchs:      sys.exit(f'::ERR Compilation from host arch: {curArch}  is not currently supported') 
## OS
posix  = bool(curPlatform in ['posix'])
win32  = bool(curPlatform in ['win32'])
#darwin = bool(curPlatform in ['darwin'])
#arm    = bool(curPlatform in ['arm'])
#arm64  = bool(curPlatform in ['arm64'])

# CLI System
cliPlatform = getCli(PLAT)   # Gets the converted value, not the raw alias input in `p=` or `platform=`
cliArch     = getCli(ARCH) 

# SCons setup
# ::::::::::::
## Verbose
if not isVerbose() and scQuiet: SetOption('no_progress',1)  # Mute SCons progress text on non-verbose mode
if isVerbose(): Progress(': Evaluating  $TARGET\n') # SCons will echo when each file is being evaluated. TODO: Chapter 9.3
## Jobs
scJobs = max(2,int(cores*coresPc))
if cores: SetOption('num_jobs', scJobs)
## Others
Decider(scDecider) # Could be assigned per environment, but using global config instead
SConsignFile(scDir.File('scons-sign').abspath)

#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


###############
# Source Code
# ::::::::::::
######################
# Client 
#::::::::
# srcClient = getGlob(engineDir, os.path.join(clDir,'*.c' ))   # All of these are relative to the src folder. But they won't contain the folder in the string
srcClient = Glob(engineDir.Dir(clDir).path  +sep+ '*.c')
srcServer = Glob(engineDir.Dir(svDir).path  +sep+ '*.c')   #   srcDir + clDir,'file.c'  ==    /absolute/to/src/cldir/file.c
srcCommon = Glob(engineDir.Dir(qcmDir).path +sep+ '*.c')
srcBot    = Glob(engineDir.Dir(botDir).path +sep+ '*.c')
# Renderer
srcRendC  = Glob(engineDir.Dir(rcDir).path  +sep+ '*.c')
srcRend1  = Glob(engineDir.Dir(r1Dir).path  +sep+ '*.c') + srcRendC
srcRendV  = Glob(engineDir.Dir(rvDir).path  +sep+ '*.c') + [f for f in srcRendC if f not in [engineDir.Dir(rcDir).File('tr_image.c')]]  #rendv = rendv + rendc - rendc_image (rc/tr_image.c)
# Unsupported renderer
srcRend2  = Glob(engineDir.Dir(r2Dir).path  +sep+ '*.c') + srcRendC   # Disabled. Renderer2 is not used
#objRend2str = """ %s/rend2/glsl/bokeh_fp.o, ETC, ETC """ % B  #todo: How do we compile *.glsl into *.o ?
## Libraries
srcJpeg   = Glob(engineDir.Dir(jpgDir).path  +sep+ '*.c')
srcSdl    = Glob(engineDir.Dir(sdlDir).path  +sep+ '*.c')
## Platform Specific
srcUnix   = Glob(engineDir.Dir(unxDir).path  +sep+ '*.c')
srcWin    = Glob(engineDir.Dir(winDir).path  +sep+ '*.c')
## Filters: Remove
srcServer_rankings    = [engineDir.Dir(svDir).File('sv_rankings.c')]
srcCommon_VMplatforms = [engineDir.Dir(qcmDir).File('vm_x86.c'), engineDir.Dir(qcmDir).File('vm_armv7l.c'), engineDir.Dir(qcmDir).File('vm_aarch64.c')]
## Filters: Add
### Linux
srcLinux = [
  engineDir.Dir(unxDir).File('unix_main.c'),
  engineDir.Dir(unxDir).File('unix_shared.c'),
  engineDir.Dir(unxDir).File('linux_signals.c'),
  ]
srcLinux_nonsdl = [
  engineDir.Dir(unxDir).File('linux_glimp.c'),
  engineDir.Dir(unxDir).File('linux_gpl.c'),
  engineDir.Dir(unxDir).File('linux_snd.c'),
  engineDir.Dir(unxDir).File('x11_dga.c'),
  engineDir.Dir(unxDir).File('x11_dga.c'),
  engineDir.Dir(unxDir).File('x11_randr.c'),
  engineDir.Dir(unxDir).File('x11_vidmode.c'),
  ]
srcLinux_qvk = [engineDir.Dir(unxDir).File('linux_qvk.c')]
### Windows
srcWin = [
  engineDir.Dir(winDir).File('win_main.c'),
  engineDir.Dir(winDir).File('win_shared.c'),
  engineDir.Dir(winDir).File('win_syscon.c'),
  #winDir,'win_resource.o', #FIXME: This is a .rc file, converted to .o with windres
  ]
srcWin_nonsdl = [
  engineDir.Dir(winDir).File('win_gamma.c'),
  engineDir.Dir(winDir).File('win_glimp.c'),
  engineDir.Dir(winDir).File('win_input.c'),
  engineDir.Dir(winDir).File('win_minimize.c'),
  engineDir.Dir(winDir).File('win_qgl.c'),
  engineDir.Dir(winDir).File('win_snd.c'),
  engineDir.Dir(winDir).File('win_wndproc.c'),
  ]
srcWin_qvk = [engineDir.Dir(winDir).File('win_qvk.c')]
### VM Platforms
srcVM_x86  = [engineDir.Dir(qcmDir).File('vm_x86.c')]
srcVM_arm  = [engineDir.Dir(qcmDir).File('vm_armv7l.c')]
srcVM_aa64 = [engineDir.Dir(qcmDir).File('vm_aarch64.c')]
### Libraries
srcCurl = [engineDir.Dir(clDir).File('cl_curl.c')]
## Shared Library dependencies
srcShlib = [
  gameDir.Dir(qcmDir).File('q_math.c'),
  gameDir.Dir(qcmDir).File('q_shared.c'),
  ]

# Target Sources: Client Engine
#::::::::::::::::::::::::::::::
srcEngine_cl = [f for f in srcClient if f not in srcCurl] \
             + [f for f in srcCommon if f not in srcCommon_VMplatforms] \
             + [f for f in srcServer if f not in srcServer_rankings] \
             + srcBot \
             + srcCurl + srcSdl
## Add Platform Specific
srcEngine_cl_lnx  = srcEngine_cl + srcLinux
srcEngine_cl_win  = srcEngine_cl + srcWin
if use_local_jpeg:  srcEngine_cl_win += srcJpeg  # Always using system libraries. Linking local jpeg on win, libjpeg binaries are hard to find
## Add Renderers
srcEngine_gl1_lnx = srcEngine_cl_lnx + srcRend1
srcEngine_gl1_win = srcEngine_cl_win + srcRend1
#srcEngine_gl2_lnx = srcEngine_cl_lnx + srcRend2 + srcRend2str
#srcEngine_gl2_win = srcEngine_cl_win + srcRend2 + srcRend2str
srcEngine_vk_lnx  = srcEngine_cl_lnx + srcRendV + srcLinux_qvk  #todo: use_vulkan_api filter needed?
srcEngine_vk_win  = srcEngine_cl_win + srcRendV + srcWin_qvk    #todo: use_vulkan_api filter needed?
if not use_opengl and not use_vulkan and not use_opengl2: sys.exit('::ERR No renderer has been defined.')
# Static version of SDL for windows (sound bug alternative)
# srcEngine_gl1_win_staticsdl = srcEngine_cl_win + srcRend1 + srcSDL_static
## Add VM & local libs
#if compile_VM:
#  if posix or win:       srcEngine_cl += srcVM_x86
#  if arm:                srcEngine_cl += srcVM_arm
#  if arm64:              srcEngine_cl += srcVM_aa64


# Target Sources: Game Both
#:::::::::::::::::::::::::::::
srcGame_phy = Glob(gameDir.Dir(phyDir).path +sep+ '*.c')

# Target Sources: Game Client
#:::::::::::::::::::::::::::::
srcCGame     = Glob(gameDir.Dir(cgDir).path  +sep+ '*.c')
srcCGame_hud = Glob(gameDir.Dir(hudDir).path +sep+ '*.c')
## Filter: Add
srcCGame_shared = [
  gameDir.Dir(sgDir).File('bg_misc.c'),
  gameDir.Dir(sgDir).File('bg_pmove.c'),
  gameDir.Dir(sgDir).File('bg_slidemove.c'),
  gameDir.Dir(sgDir).File('bg_lib.c'),
  ]
## Filters: Apply
srcGame_cg = srcCGame +srcCGame_shared +srcCGame_hud +srcGame_phy +srcShlib


# Target Sources: Game Server
#::::::::::::::::::::::::::::
srcSGame    = Glob(gameDir.Dir(sgDir).path +sep+ '*.c')
## Filter: Remove
srcSGame_rankings = [gameDir.Dir(sgDir).File('g_rankings.c')]
## Filters: Apply
srcGame_sg = [f for f in srcSGame if f not in srcSGame_rankings] + srcGame_phy + srcShlib


# Target Sources: Game UI
#:::::::::::::::::::::::::
srcUI = Glob(gameDir.Dir(uiDir).path +sep+ '*.c')
## Filter: Add
srcUI_bg = [
  gameDir.Dir(sgDir).File('bg_misc.c'),
  gameDir.Dir(sgDir).File('bg_lib.c'),
  ]
## Filter: Remove
srcUI_rankings = [
  gameDir.Dir(uiDir).File('ui_login.c'),
  gameDir.Dir(uiDir).File('ui_rankings.c'),
  gameDir.Dir(uiDir).File('ui_rankstatus.c'),
  gameDir.Dir(uiDir).File('ui_signup.c'),
  gameDir.Dir(uiDir).File('ui_specifyleague.c'),
  gameDir.Dir(uiDir).File('ui_spreset.c'),
  ]
## Filters: Apply
srcGame_ui = [f for f in srcUI if f not in srcUI_rankings] +srcUI_bg +srcShlib

## Ui New
srcUI_core  = Glob(gameDir.Dir(nuiDir).Dir('c').path +sep+ '*.c')
srcUI_menu  = Glob(gameDir.Dir(nuiDir_menu).Dir('c').path +sep+ '*.c')
srcUI_fwork = Glob(gameDir.Dir(nuiDir_fwork).Dir('c').path +sep+ '*.c')
srcUI_color = Glob(gameDir.Dir(nuiDir_color).Dir('c').path +sep+ '*.c')
srcGame_nui = srcUI_core +srcShlib +srcUI_menu +srcUI_fwork +srcUI_color# +srcUI_bg

#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


###############
# Construction Data
#:::::::::::::::::::

# Base: Build Data
#::::::::::::::::::
base = BuildObject()  # Don't use base.setup(). This just stores build data     # Superclass possible, but #todo(maybe)
## Output data
base.bindir   = binDir  # Folder where the binaries will be created
## Compiler
### CC FLAGS
base.ccflags  = CCFLAGS_base
base.ccflags += [
      # Default in Q3 makefile:
    '-Wall','-Wstrict-prototypes', 
    #'-Wno-unused-result',  # I don't think this is needed. Mark functions as void instead??
    #'-pipe',               # Used to be a performance increase back in the day. Probably unnoticeable now, and it can create issues
    #'-Wimplicit',          # Default in Q3, but implied by -Wall
    '-m64',                 # 64bit math. q3e has it only on win, but should be base  #todo: support for 32bit?
      # ioq3 only
    '-fno-strict-aliasing', #TODO: Does this break odfe?
      # New by sOkam:
    #'-std=c99', '-Wpedantic', '-pedantic', #'-pedantic-errors', # Enforce ISO C89 standard. Probably want 99
    #'-Wdouble-promotion',  # Warn when a float is promoted to double
      # Recommended in tutorial:
    #'-Wextra',#'-Werror',
    #'-Wmissing-prototypes','-Wmisleading-indentation','-Wduplicated-cond','-Wold-style-definition',
    #'-Wconversion','-Wshadow','-Winit-self','-Wfloat-equal','-Wcast-align=strict',
    #'-Wformat-overflow=2','-Wformat-truncation=2', #'-Wformat', # Implied in -Wall
    # '-fdiagnostics-format=text','-fdiagnostics-minimum-margin-width=5',#'-Wwrite-strings',
    ]
### General Defines
base.defines  = DFLAGS_base
base.defines += ['BOTLIB']
### Libraries
#### Defines
#### Linking
base.libs     = LIBS_base
base.libs    += ['m']         # -lm
base.ldflags  = LDFLAGS_base
base.ldflags += ['-Wl,--gc-sections', '-fvisibility=hidden']
#### Include Paths
base.ccpath   = CCPATH_base
base.parse    = PARSE_base # Will be setup as env.ParseConfig('value')


# Release: Build Data
#:::::::::::::::::::::
rls = BuildObject()   # Don't use rls.setup(). This just stores build data
## Compiler
### Compiler FLAGS
rls.ccflags += CCFLAGS_rls
rls.ccflags += [
    '-O2',                 # Perform all optimizations that do not involve a space-speed tradeoff. Default -O0
    '-fvisibility=hidden', # Make all symbols hidden, unless specified (private, static, public, etc)]
    '-s',                  # Strip all symbols from the binary
    #'-ffast-math',         # Less precise, but faster, math
    ]  #todo: Test and research -O3 and -Ofast. Could be faster than -O2. ** q3e -ffast-math only win. ioquake3 -O3 and -ffast-math everywhere
rls.ctype = 'release'
### General Defines
rls.defines += ['NDEBUG']


# Debug: Build Data
#::::::::::::::::::
dbg = BuildObject()    # Don't use dbg.setup(). This just stores build data
## Compiler
dbg.ctype = 'debug'
### Compiler FLAGS
dbg.ccflags += CCFLAGS_dbg
dbg.ccflags += [
    '-Og', # Optimize -O1, but without incompatible debugging flags. Best option for fast building + debug
    '-g3', # Generate Debug information, including all extras
    ]
### General Defines
dbg.defines += ['DEBUG', '_DEBUG']


# Linux: Build Data
#:::::::::::::::::::
lnx = BuildObject()     # Don't use lnx.setup(). This just stores build data
## Environment & System
lnx.plat = 'posix'
lnx.arch = 'x86_64'
## Compiler
#### Linking
lnx.libs    += ['dl']        # -ldl
lnx.ldflags += ['-Wl,--hash-style=both']
#### Include Paths
lnx.ccpath   = [os.path.join('/usr','include'), os.path.join('/usr','local','include')]  #TODO: This shouldn't be needed, but.... is it?


# Windows: Build Data
#:::::::::::::::::::::
win = BuildObject()      # Don't use win.setup(). This just stores build data
## Environment & System
win.plat     = 'win32'
win.arch     = 'x86_64'
## Compiler
win.defines += ['WINVER=0x501']  # Require WinXP or later
win.libs     = ['wsock32','gdi32','winmm','ole32','ws2_32','psapi','comctl32']
win.ldflags  = ['-mwindows', '-Wl,--dynamicbase', '-Wl,--nxcompat',]

# def DO_WINDRES (trg, src):  #TODO: How do we add Icon, q3.manifest and the other data defined in code/win32/win_resource.rc ??
#   WINDRES_cmd -i src -o trg


# Client: Build Data
#::::::::::::::::::
cl         = base.clone()
## Output data
cl.bintype = 'bin'
### Custom compiler name (non verbose)
cl.cStr    = 'CC'
cl.lStr    = 'LD'
## Source code
cl.srcdir  = engineDir 
### General Defines
cl.defines += ['NO_VM_COMPILED']
if baseDir:         cl.defines += [f'DEFAULT_BASEDIR="{baseDir}"']
if use_vulkan_api:  cl.defines += [ 'USE_VULKAN_API']
### Libraries
#### Defines
# We are not using local headers at all. We use system libraries always. And we are not using dlopen versions of any
#if use_local_headers:  client.defines += ['USE_LOCAL_HEADERS=1']  #TODO: Revisit this after first rls.game playtest
cl.defines += ['USE_PCRE']
cl.defines += ['USE_CURL']
#### Linking
cl.libs    += ['SDL2']
cl.libs    += ['pcre']
cl.libs    += ['curl']
cl.libs    += ['jpeg']
#:::::::::::::::::::::::::::::::

## Linux
#:::::::::::::::::::::::::::::::
clLnx          = cl.merge(lnx)
clLnx.src      = srcEngine_gl1_lnx
clLnx.binname = f'{cName}{"."+clLnx.arch if clLnx.arch in ["x86"] else ".x64"}'
### Defines
clLnx.defines += ['USE_ICON']
clLnx.defines += ['USE_SYSTEM_JPEG']
### Include Paths
if posix:      clLnx.parse += ['pkg-config --silence-errors --cflags-only-I sdl2']
# clLnx.defines += [f'ARCH_STRING="{clLnx.arch}"']
## Create BuildObjects: Release and Debug
clLnx_rls = clLnx.merge(rls)
clLnx_dbg = clLnx.merge(dbg)
#:::::::::::::::::::::::::::::::

## Windows
#:::::::::::::::::::::::::::::::
clWin          = cl.merge(win)
clWin.src      = srcEngine_gl1_win
clWin.binname = f'{cName}{"-"+clWin.arch if clWin.arch in ["x86"] else "-x64"}'
### Defines
clWin.defines += ['USE_ICON']
if not use_local_jpeg: clWin.defines += ['USE_SYSTEM_JPEG']
# clWin.defines += [f'ARCH_STRING="{clWin.arch}"']
# SDL extras
# if posix:      clWin.parse += ['pkg-config --silence-errors --cflags sdl2 | sed "s/-Dmain=SDL_main//"']
# CURL extras
clWin.defines += ['CURL_STATICLIB']
clWin.libs    += ['wldap32','crypt32'] # Curl support libs
clWin.ccpath  += [os.path.join(engineDir.abspath,'lib','curl','windows','include')]
clWin.libpath += [os.path.join(engineDir.abspath,'lib','curl','windows','mingw','lib64')]
# PCRE extras
clWin.ccpath  += [os.path.join(engineDir.abspath,'lib','pcre','windows','include')]
clWin.libpath += [os.path.join(engineDir.abspath,'lib','pcre','windows','mingw','lib64')]
## Create BuildObjects: Release and Debug
clWin_rls = clWin.merge(rls)
clWin_dbg = clWin.merge(dbg)
#:::::::::::::::::::::::::::::::

# SDL static version (win only)
#:::::::::::::::::::::::::::::::
clWin_sdl = clWin_rls.clone()
clWin_sdl.binname += '-staticsdl'
clWin_sdl.ssdir    = 'staticsdl'
clWin_sdl.defines += ['USE_LOCAL_HEADERS=1']
clWin_sdl.ccpath  += [os.path.join(engineDir.abspath,'lib','sdl','windows','include','SDL2')]
clWin_sdl.libpath += [os.path.join(engineDir.abspath,'lib','sdl','windows','mingw','lib64')]
clWin_sdl.libs     = [lib for lib in clWin_sdl.libs if lib not in ['SDL2']] + ['SDL264']
clWin_sdl.ldflags += ['-static']
#todo Might be only for dynamic linking, and not need this file
libSDL2 = engineDir.Dir('lib').Dir('sdl').Dir('windows').Dir('mingw').Dir('lib64').File('SDL264.dll')
#:::::::::::::::::::::::::::::::


# Game Client: Build Object
#:::::::::::::::::::::::::::
cg         = base.clone()
## Source code
cg.src     = srcGame_cg
cg.srcdir  = gameDir 
cg.ssdir   = 'cgame'
# Custom compiler name (non verbose)
cg.cStr    = 'CC_cgame'
cg.lStr    = 'LD_cgame'
# Output data
cg.bintype = 'lib'
# Add defines
#cg.defines += ['CGAME']

# Linux
cgLnx = cg.merge(lnx)
cgLnx.binname = f'cgame{cgLnx.arch}'
cgLnx.defines += [f'ARCH_STRING="{cgLnx.arch}"']
cgLnx_rls = cgLnx.merge(rls)
cgLnx_dbg = cgLnx.merge(dbg)
# Windows
cgWin = cg.merge(win)
cgWin.binname = f'cgame{cgWin.arch}'
cgWin.defines += [f'ARCH_STRING="{cgWin.arch}"']
cgWin_rls = cgWin.merge(rls)
cgWin_dbg = cgWin.merge(dbg)


# Game Server: Build Object
#:::::::::::::::::::::::::::
sg         = base.clone()
## Source code
sg.src     = srcGame_sg
sg.srcdir  = gameDir 
sg.ssdir   = 'sgame'
# Output data
sg.bintype = 'lib'
# Add defines
#sg.defines += ['QAGAME']

# Linux
sgLnx = sg.merge(lnx)
sgLnx.binname = f'qagame{sgLnx.arch}'
sgLnx.defines += [f'ARCH_STRING="{sgLnx.arch}"']
sgLnx_rls = sgLnx.merge(rls)
sgLnx_dbg = sgLnx.merge(dbg)
# Windows
sgWin = sg.merge(win)
sgWin.binname = f'qagame{sgWin.arch}'
sgWin.defines += [f'ARCH_STRING="{sgWin.arch}"']
sgWin_rls = sgWin.merge(rls)
sgWin_dbg = sgWin.merge(dbg)


# Game UI: Build Object
#:::::::::::::::::::::::::::
ui         = base.clone()
## Source code
ui.src     = srcGame_ui
ui.srcdir  = gameDir 
ui.ssdir   = 'ui'
# Custom compiler name (non verbose)
ui.cStr    = 'CC_ui'
ui.lStr    = 'LD_ui'
# Output data
ui.bintype = 'lib'
# Add defines
#ui.defines += ['UI']

# Linux
uiLnx = ui.merge(lnx)
uiLnx.binname = f'ui{uiLnx.arch}'
uiLnx.defines += [f'ARCH_STRING="{uiLnx.arch}"']
uiLnx_rls = uiLnx.merge(rls)
uiLnx_dbg = uiLnx.merge(dbg)
# Windows
uiWin = ui.merge(win)
uiWin.binname = f'ui{uiWin.arch}'
uiWin.defines += [f'ARCH_STRING="{uiWin.arch}"']  #TODO: Fix Memory Address nonsense. This is already defined???
uiWin_rls = uiWin.merge(rls)
uiWin_dbg = uiWin.merge(dbg)

#:::::::::::::::::::::::::::::::
# Ui New
uiNew = uiLnx_rls.clone()
uiNew.src = srcGame_nui
uiNew.ssdir = 'nui'
uiNew.binname = 'n'+uiNew.binname
#:::::::::::::::::::::::::::::::


# Setup BuildObjects with SCons
#:::::::::::::::::::::::::::::::
## Engine: Client
clWin_rls.setup()
clWin_dbg.setup()
clLnx_rls.setup()
clLnx_dbg.setup()
clWin_sdl.setup()
## Game: Client
cgLnx_rls.setup()
cgLnx_dbg.setup()
cgWin_rls.setup()
cgWin_dbg.setup()
## Game: Server
sgLnx_rls.setup()
sgLnx_dbg.setup()
sgWin_rls.setup()
sgWin_dbg.setup()
## Game: UI
uiLnx_rls.setup()
uiLnx_dbg.setup()
uiWin_rls.setup()
uiWin_dbg.setup()
## Ui New
uiNew.setup()


#############################################################################
# DEDICATED SERVER
#############################################################################
#srcDed = """
#  # Server
#    # Missing sv_rankings.c
#  # Common
#    # Missing qcommon/puff.c
#    # Missing vm_ Platforms
#  # Botlib
#  """ % binDir

#if mingw: srcDed += srcWin
#else:     srcDed += srcLinux
#if compile_VM:
#  if trgArch in ['x86', 'x86_64']:  srcDed += srcVM_x86
#  if arm:              srcDed += srcVM_arm
#  if trgArch == 'aarch64':          srcDed += srcVM_aa64

# Server Objects
#::::::::::::::::
#DO_AS      ( binDir/ded/%.o,  asmDir/%.s )
#DO_DED_CC  ( binDir/ded/%.o,  svDir/%.c  )
#DO_DED_CC  ( binDir/ded/%.o,  qcmDir/%.c )
#DO_DED_CC  ( binDir/ded/%.o,  botDir/%.c )
## Platform Specific
#DO_DED_CC  ( binDir/ded/%.o,  unxDir/%.c )
#DO_DED_CC  ( binDir/ded/%.o,  winDir/%.c )
#DO_WINDRES ( binDir/ded/%.o,  winDir/%.rc)

# Server Binaries
#::::::::::::::::
#binDir/trgDed: objDed
#	CC_cmd -o THIS_trgFilename objDed LDFLAGS_base
#compile(lnkDir,''+trgDed, env=rls, cStr='DED_CC')

#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################


#########
# Keywords
#::::::::::::::::::
###############################
#build_server = build_server or bool(server in COMMAND_LINE_TARGETS)
trgBuildList = getCli(TRG)

# # Compile Keywords
# ## Target assignment
# # engine
trgEngine_ld = [clLnx_dbg.trg]
trgEngine_lr = [clLnx_rls.trg]
trgEngine_wd = [clWin_dbg.trg]
trgEngine_wr = [clWin_rls.trg]
trgEngine_ws = [clWin_sdl.trg]
# # server
# #TODO add server (here and trgAll)
# # game
trgGame_ld   = [cgLnx_dbg.trg, sgLnx_dbg.trg, uiLnx_dbg.trg]
trgGame_lr   = [cgLnx_rls.trg, sgLnx_rls.trg, uiLnx_rls.trg]
trgGame_wd   = [cgWin_dbg.trg, sgWin_dbg.trg, uiWin_dbg.trg]
trgGame_wr   = [cgWin_rls.trg, sgWin_rls.trg, uiWin_rls.trg]
# # all
trgAll       = [trgEngine_ld, trgEngine_lr, trgEngine_wd, trgEngine_wr, trgEngine_ws,
                trgGame_ld, trgGame_lr, trgGame_wd, trgGame_wr]
# WIP
trgUiQ3      = [uiLnx_rls.trg]
trgUiNew     = [uiNew.trg]
Alias('q3ui', trgUiQ3)
Alias('nui', trgUiNew)

## Groups
### Current Platform
# engine
if   posix: Alias('engine', trgEngine_lr)
elif win32: Alias('engine', trgEngine_wr)
if   posix: Alias('engine-dbg', trgEngine_ld)
elif win32: Alias('engine-dbg', trgEngine_wd)
Alias('engine-dist', trgEngine_lr + trgEngine_wr)
Alias('engine-sdl', trgEngine_ws)
# game
if   posix: Alias('game', trgGame_lr)
elif win32: Alias('game', trgGame_wr)
if   posix: Alias('game-dbg', trgGame_ld)
elif win32: Alias('game-dbg', trgGame_wd)
Alias('game-dist', trgGame_lr + trgGame_wr)
# both
Alias('release', ['engine', 'game'])
Alias('debug', ['engine-dbg', 'game-dbg'])
Alias('distribute', ['engine-dist', 'game-dist'])  #TODO add server
# all
Alias('all', trgAll)


# Default targets
#::::::::::::::::::
# What to build when no targets are selected (additive, doesn't replace, list or strings)
if not build_default:  Default('debug')       # Build debug if list is empty
else:                  Default(build_default) # Assign the list to SCons build defaults
#:::::::::::::::::::::::::::::::::::::::::::::::::::::::::: 
############################################################

